private _unit = _this select 0;
private _cercano = _this select 1;
private _cuenta = {_x == APERSMineMag} count (magazines _unit);
if (_cuenta == 0) exitWith {};
_index = allMinesCFG findIf {_x select 1 == APERSMineMag};
if (_index == -1) exitWith {};
_tipoMina = (allMinesCFG select _index) select 0;
_unit call A3A_fnc_recallGroup;
_unit setVariable ["maniobrando",true];
_ang = _unit getDir _cercano;
private _posRef = _unit getPos [50,_ang];
_posRef = _posRef findEmptyPosition [0,50,(typeOf _unit)];
_unit disableAI "TARGET";
_unit doMove _posRef;
private _timer = time + 60;
waitUntil {sleep 1;(_unit distance _posRef <= 1) or !([_unit] call A3A_fnc_canFight) or (time > _timer)};
_unit enableAI "TARGET";
if (_unit distance _posRef > 1) exitWith {_unit setVariable ["maniobrando",false]};
_unit playActionNow "PutDown";
_mina = createMine [_tipoMina,_posRef,[],0];
side (group _unit) revealMine _mina;
_unit removeMagazine APERSMineMag;
_unit setVariable ["maniobrando",false];
_unit doFollow (leader _unit);